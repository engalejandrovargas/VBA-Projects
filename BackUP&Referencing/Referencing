Solution to Referencing Ranges

Sub ReferToCells()
    '_______________________________________________
    '
    'Write to one or more cells. Make sure you run this in the correct sheet
    '_______________________________________________
    
    Cells.Clear
    Range("A1").Value = "1st"  'Cells(1, 1).Value = "1st"
    
    Range("A2:C2").Value = "2nd"
    
    Range("A3:C3,E3:F3").Value = "3rd"
    
    'pastes only in A4 and C4 but not in B4
    Range("A4,C4") = "4th"
    
    'pastes from A5 to C5 including B5
    Range("A5", "C5") = "5th"
    
    Range("A" & 6, "C" & 6) = "6th"   'Range(Cells(6, 1), Cells(6, 3)).Value = "6th"
    
    'uses the cells property of the range object
    Range("A4:C7").Cells(4, 2).Value = "7th"
    
    'uses the offset property
    Range("A1").Offset(7, 2).Value = "8th"
    
    'offset can be used on more than one cell
    Range("A1:B1").Offset(8, 1).Value = "9th"
    
    'names in name manager can be used
    'Lastone is a name in name manager
    
    Range("LastOne").Value = "10th"
    
    '_______________________________________________
    '
    'Refer to entire rows or columns
    '_______________________________________________
    
    Rows("12:14").RowHeight = 30
    
    'rows 17 & 19 are not impacted
    Range("16:16,18:18,20:20").RowHeight = 30
    
    Columns("E:F").ColumnWidth = 10
    Range("H:H,J:J").ColumnWidth = 10
    Range(Columns(1), Columns(3)).ColumnWidth = 5
    
    'to autofit all columns
    'Cells.Columns.AutoFit
   
End Sub

Sub Range_Property_Method_Examples()

    'Example of Property --> Text Property
    '--------------------------------------
    
    Range("B6").Value = Range("B5").Value
    'VBA sees this as text
    Range("B7").Value = Range("B5").Text
    'Debug.Print TypeName(Range("B5").Text)
    'Debug.Print TypeName(Range("B5").value)
    
    '--------------------------------------
    'Example of Method --> Delete
    '--------------------------------------
    Range("B6").Delete xlToLeft
    Range("B9").EntireRow.Delete
    
    '--------------------------------------
    'Select cell A1 on Tab "Purpose"
    '--------------------------------------
    Worksheets("purpose").Activate
    Range("A1").Select
    
End Sub


Sub Range_LastRow()

    'Color last row
    'clear the answer area first and reset color of range area
    Range("K6:K12").ClearContents
    Range("A5", "A" & Cells(Rows.Count, 1).End(xlUp).Row).EntireRow.Interior.Color = Excel.Constants.xlNone

    'find answers
    ' ****** Last row  *******
     'Range("K6").Value = Range("A4").End(xlDown).Row
     Range("K6").Value = Range("A" & Rows.Count).End(xlUp).Row 'Range("K6").Value = Cells(Rows.Count, 1).End(xlUp).Row
    Range("K7").Value = Range("A" & Rows.Count).End(xlUp).Row + 1 'Cells(Rows.Count, 1).End(xlUp).Row + 1
    
     ' ****** Last column  *******
     Range("K8").Value = Cells(4, Columns.Count).End(xlToLeft).Column  'Range("A4").End(xlToRight).Column
     
      ' ****** Current region  *******
     'Debug.Print Range("B10").CurrentRegion.Address
    Range("K9").Value = Range("B10").CurrentRegion.Address
    Range("K10").Value = Range("B10").CurrentRegion.Rows.Count
    
     ' ****** Last used row on sheet *******
    'to find last used cell use specialcells property
    Range("K11").Value = Cells.SpecialCells(xlCellTypeLastCell).Row
    
    'usedrange is a property of the worksheet
    Range("K12").Value = Application.ActiveSheet.UsedRange.Rows.Count
    'to get intellisense, use the sheet code name - we learn this in the referencing worksheets lecture
    Range("K12").Value = Sheet4.UsedRange.Rows.Count
    
    'set the color of the last row to red
    Range("A" & Rows.Count).End(xlUp).EntireRow.Interior.Color = VBA.ColorConstants.vbRed


End Sub

Sub Copy_Resize_Ranges()

    'this macro shows you how to copy ranges and resize them if needed
    'clear before copy
    Range("J4:O16,J20:O32,A20:O32").Clear

     '-------------------------
    'Copy Method
    '-------------------------
    Range("A4:E10").Copy Range("J4")
    Range("A4").CurrentRegion.Copy Range("J4")

    '-------------------------
    'PasteSpecial Method
    '-------------------------
    Range("A4").CurrentRegion.Copy
    Range("J20").PasteSpecial xlPasteValuesAndNumberFormats
    Range("J20").PasteSpecial xlPasteComments

    '-------------------------
    'Resize Property with Copy Method
    '-------------------------
    Range("A4").CurrentRegion.Offset(1, 0).Resize(Range("A4"). _
    CurrentRegion.Rows.Count - 1).Copy
    Range("A20").PasteSpecial xlPasteValuesAndNumberFormats
    Application.CutCopyMode = False
    Range("A1").Select
    

    
End Sub

Sub Reference_Worksheet()

    'this macro shows you how to best reference worksheets
    
    Worksheets("Worksheet").Range("A3").Value = "I will copy data here"
    
    ShDest.Range("A3").Value = "I will copy data here"
    
    ShSource.Range("A4").CurrentRegion.Copy ShDest.Range("A4")
    
    'Debug.Print ActiveSheet.Name

End Sub

Sub Reference_Workbook()

    'this macro shows you how to best reference workbooks

    'open workbook first if not open --> make sure you put the correct path
    'later I will show you how can pause the macro and allow the user to open the workbook

    'Application.Workbooks.Open "C:\Desktop\DestBook.xlsx"

    Workbooks("Destbook.xlsx").Worksheets(1).Range("A3").Value = "I will copy data here too"
    Debug.Print Workbooks("DestBook").FullName
    Debug.Print Workbooks(2).FullName
    Debug.Print ThisWorkbook.Path

    ThisWorkbook.Worksheets("Copy_ReSize").Range("A4").CurrentRegion.Copy _
    Workbooks("DestBook.xlsx").Worksheets(1).Range("A4")
    'the below does the same as above, except it uses the codename

    ShSource.Range("A4").CurrentRegion.Copy _
    Workbooks("DestBook.xlsx").Worksheets(1).Range("A4")
    

    'to save a copy of this workbook
    'ThisWorkbook.SaveCopyAs ThisWorkbook.Path & "\S4_Backup.xlsm"
    
    
End Sub
